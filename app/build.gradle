buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

plugins {
    id "com.android.application"
    id "org.jlleitschuh.gradle.ktlint"

    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-allopen"
    id "kotlin-parcelize"

    id "com.google.firebase.crashlytics"

    id "pmd"
    id "checkstyle"
    id "jacoco"
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId 'com.atomicrobot.carbon'

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true

        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField "String", "VERSION_FINGERPRINT", rootProject.ext.versionFingerprint

        proguardFile getDefaultProguardFile('proguard-android.txt')
        proguardFile 'proguard-rules.pro'

        testInstrumentationRunner 'com.atomicrobot.carbon.CustomAppTestRunner'
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        // If you are creating signing keys, consider setting up Google Play App Signing!
        // See: https://developer.android.com/studio/publish/app-signing.html#google-play-app-signing
        release {
            apply from: rootProject.file('distribution/keys/sample.gradle')

            storeFile rootProject.file(sampleKeystore)
            storePassword sampleKeystorePassword
            keyAlias sampleKeyAlias
            keyPassword sampleKeyPassword
        }
        // Use debug.keystore in this project so that debug version works with AR's Carbon web link
        // setup. You can safely remove this section if you are not using web linking within your app
        debug {
            storeFile rootProject.file('distribution/keys/debug.keystore')
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    flavorDimensions "app"
    productFlavors {
        dev {
            dimension "app"
            applicationId 'com.atomicrobot.carbon.dev'
        }
        prod {
            dimension "app"
            applicationId 'com.atomicrobot.carbon'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            testCoverageEnabled true
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }


    dataBinding {
        enabled = true
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.atomicrobot.carbon'
    lint {
        abortOnError true
        htmlReport true
        lintConfig file('lint.xml')
    }
}

ext {
    // App dependency versions
    appCompatVersion = '1.5.0'
    supportVersion = '1.0.0'
    annotationVersion = '1.4.0'
    playServicesVersion = '18.1.0'
    lifecycleVersion = '2.0.0'
    lifecycleRuntimeVersion = '2.4.1'
    koinVersion = '3.2.0'
    retrofitVersion = '2.9.0'
    okHttpVersion = '4.9.1'
    moshiVersion = '1.13.0'
    coreVersion = '1.7.0'
    constraintLayoutVersion = '2.1.4'
    recyclerViewVersion = '1.2.1'
    materialVersion = '1.6.1'
    rxJavaVersion = '2.2.21'
    rxAndroidVersion = '2.1.1'
    timberVersion = '5.0.1'
    leakCanaryVersion = '2.7'

    // Test dependency versions
    mockitoVersion = '4.4.0'
    mockitoKotlinVersion = '1.6.0'
    robolectricVersion = '4.7.3'
    androidTestSupportVersion = '1.4.0'
    espressoVersion = '3.4.0'
    junitVersion = '4.13.2'
    junitTestVersion = '1.1.3'

    mlBarcodeScannerVersion = '17.0.2'
    camerax_version = '1.2.0-alpha04'
    splashVersion = "1.0.0"
}

dependencies {
    implementation platform("com.google.firebase:firebase-bom:$rootProject.ext.firebase_bom_version")
    implementation "com.google.firebase:firebase-analytics-ktx"

    // Add the Firebase Crashlytics SDK.
    implementation "com.google.firebase:firebase-crashlytics:$rootProject.ext.firebase_crashlytics_version"

    // Recommended: Add the Google Analytics SDK.
    implementation "com.google.firebase:firebase-analytics:$rootProject.ext.firebase_analytics_version"

    implementation "androidx.compose.ui:ui:$compose_version"
    // Compose Material Design
    implementation "androidx.compose.material:material:$compose_version"
    // Animations
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    // Integration with activities
    implementation "androidx.activity:activity-compose:$compose_act_version"

    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    // Tooling support (Previews, etc.)
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$compose_vm_version"
    // UI Tests
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    implementation "androidx.compose.foundation:foundation:$compose_foundation_verison"

    implementation "androidx.compose.material:material-icons-extended:$compose_foundation_verison"

    /*
     * Starting from Kotlin 1.4 the Kotlin standard library dependency doesn't need to be added
     * explicitly. An implicit dep. w/ the same version as the Kotlin Gradle plugin will
     * implicitly be added.
     *
     * link: https://stackoverflow.com/a/64988522/3591491
     */
    /*implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"*/
    kapt "androidx.databinding:databinding-compiler:$rootProject.ext.android_plugin_version"
    // Need this because of Kotlin

    // Android/Google libraries
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.cardview:cardview:$supportVersion"
    implementation "androidx.annotation:annotation:$annotationVersion"
    implementation "com.google.android.material:material:$materialVersion"

    implementation "com.google.android.gms:play-services-base:$playServicesVersion"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleRuntimeVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    // App architecture - Koin
    implementation "io.insert-koin:koin-android:$koinVersion"
    implementation "io.insert-koin:koin-androidx-compose:$koinVersion"

    // App architecture - RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // JSON
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // Navigation
    implementation "androidx.navigation:navigation-compose:$compose_nav_verison"
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$navigation_version"

    // Networking - HTTP
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion"

    // Networking - REST
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    // Monitoring - Timber (logging)
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Monitoring - Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

    // Unit test
    testImplementation "junit:junit:$junitVersion"
    testImplementation "androidx.test:rules:$androidTestSupportVersion"
    testImplementation "androidx.test:core:$androidTestSupportVersion"
    testImplementation "androidx.test.ext:junit:$junitTestVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlinVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"


    // Android JUnit Runner, JUnit Rules, and Espresso
    // Android JUnit Runner, JUnit Rules, and Espresso
    androidTestImplementation "androidx.test:runner:$androidTestSupportVersion"
    androidTestImplementation "androidx.test:rules:$androidTestSupportVersion"
    androidTestImplementation "androidx.test:core:$androidTestSupportVersion"
    androidTestImplementation "androidx.test.ext:junit:$junitTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"

    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlinVersion"

    // Use this dependency to bundle the barcode-scanner model with the app
    implementation "com.google.mlkit:barcode-scanning:$mlBarcodeScannerVersion"

    // The following line is optional, as the core library is included indirectly by camera-camera2
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    // If you want to additionally use the CameraX Lifecycle library
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    // If you want to additionally use the CameraX View class
    implementation "androidx.camera:camera-view:${camerax_version}"
    // If you want to additionally add CameraX ML Kit Vision Integration
    implementation "androidx.camera:camera-mlkit-vision:${camerax_version}"
    // If you want to additionally use the CameraX Extensions library
    implementation "androidx.camera:camera-extensions:${camerax_version}"

    implementation "androidx.constraintlayout:constraintlayout-compose:${compose_constraint_version}"
    implementation "androidx.core:core-splashscreen:${splashVersion}"

    // Room Rb
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // To use Kotlin annotation processing tool (kapt)
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.room:room-ktx:$room_version"
}


task pmd(type: Pmd, dependsOn: "assembleDebug") {
    ruleSetFiles = files("${project.rootDir}/config/pmd/pmd-ruleset.xml")
    ruleSets = []
    // See http://sourceforge.net/p/pmd/discussion/188193/thread/6e9c6017/ for why this is needed...
    source = fileTree('src/main/java/')
    exclude '**/gen/**'
    reports {
        // html.enabled = true
        // xml.enabled = false
        xml.required.set(false)
        html.required.set(true)
    }
}

task checkstyle(type: Checkstyle, dependsOn: "assembleDebug") {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**', '**/test/**', '**/androidTest/**'
    reports {
        // xml.enabled false
        // html.enabled true
        xml.required.set(false)
        html.required.set(true)
    }
    classpath = files(file("${project.rootDir}/app/build/intermediates/classes"))
    configProperties = [
            'checkstyle.cache.file': rootProject.file('build/checkstyle.cache'),
    ]
}

jacoco {
    toolVersion = rootProject.ext.jacoco_version
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDevDebugUnitTest', 'createDevDebugCoverageReport']) {
    reports {
        // xml.enabled = true
        // html.enabled = true
        xml.required.set(true)
        html.required.set(true)
    }

    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            /* Parcelize */
            '**/*Creator.*',
            /* Data binding */
            '**/*Binding*.*',
            '**/BR.**'
    ]

    getClassDirectories().setFrom(fileTree(
            // Java generated classes on Android project (debug build)
            dir: "$buildDir/intermediates/classes/dev/debug",
            excludes: excludes
    ) + fileTree(
            // Kotlin generated classes on Android project (debug build)
            dir: "$buildDir/tmp/kotlin-classes/devDebug",
            excludes: excludes
    ))

    getSourceDirectories().setFrom(files([
            "src/main/java",
            "src/main/kotlin"
    ]))

    getExecutionData().setFrom(fileTree(dir: project.buildDir, includes: [
            'jacoco/testDevDebugUnitTest.exec',
            'outputs/code-coverage/connected/**/*coverage.ec'
    ]))
}

// Kotlin plugin for testing
allOpen {
    annotation("com.atomicrobot.carbon.Mockable")
}

apply plugin: 'com.google.gms.google-services'
